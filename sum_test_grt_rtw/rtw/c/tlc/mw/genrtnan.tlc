%%
%%
%%
%% Abstract:
%%      TLC file for generating NaN via Target Function Library (TFL).
%%
%% Copyright 2003-2023 The MathWorks, Inc.

%implements "genrtnan" "C"

%% Function: FcnLittleEndian =======================================================================
%% Abstract:
%%      Setup code for nonfinte values on little-endian processors
%%
%function FcnLittleEndian() Output
    union {
        LittleEndianIEEEDouble bitVal;
        %<LibGetDataTypeNameFromId(::CompiledModel.tSS_DOUBLE)> fltVal;
    } tmpVal;

    %% Using MATLAB values for NaN rather than IEEE
    tmpVal.bitVal.words.wordH = 0xFFF80000U;
    tmpVal.bitVal.words.wordL = 0x00000000U;
    nan = tmpVal.fltVal;
%endfunction

%% Function: FcnBigEndian ==========================================================================
%% Abstract:
%%      Setup code for nonfinte values on big-endian processors
%%
%function FcnBigEndian() Output
    union {
        BigEndianIEEEDouble bitVal;
        %<LibGetDataTypeNameFromId(::CompiledModel.tSS_DOUBLE)> fltVal;
    } tmpVal;

    tmpVal.bitVal.words.wordH = 0x7FFFFFFFU;
    tmpVal.bitVal.words.wordL = 0xFFFFFFFFU;
    nan = tmpVal.fltVal;
%endfunction

%% Function: rtGetNaNF =============================================================================
%% Abstract:
%%      GENERATE entry point for Target Function Library TLC callbacks. Creates
%%      header and source file contents for rt_getNaNF.
%%
%function rtGetNaNF(fctInfo,fileH,fileC) void
    %<rtGetNaN(fctInfo,fileH,fileC)>
%endfunction

%% Function: rtGetNaN ==============================================================================
%% Abstract:
%%      GENERATE entry point for Target Function Library TLC callbacks. Creates
%%      header and source file contents for rt_getNaN.
%%
%function rtGetNaN(fctInfo,fileH,fileC) void

    %if SLibSupportNonfiniteLiterals() && !IsModelReferenceSimTarget()
        %% START_ASSERT
        %assign msg = "Generating nonfinites from TLC"...
            "Should be generating them from C++"
        %<LibReportFatalError(msg)>
        %% END_ASSERT
    %endif

    %% Create header file contents
    %openfile buf
    /*
    * %<fctInfo.FileName>.h
    *
    %<SLibCommonUtilHeaderInfo()>\
    */
    %closefile buf
    %<SLibCacheUtilCodeToFile("util_hdr_banner", fileH, buf)>

    %% For size_t.
    %<SLibCacheUtilCodeToFile("util_hdr_reqed_incl", fileH, "<stddef.h>")>
    %openfile buf
    %<LibEmitPlatformTypeIncludes()>
    #include "rt_nonfinite.h"

    %closefile buf
    %<SLibCacheUtilCodeToFile("util_hdr_incl", fileH, buf)>


    %assign doubleName = LibGetDataTypeNameFromId(::CompiledModel.tSS_DOUBLE)
    %assign singleName = LibGetDataTypeNameFromId(::CompiledModel.tSS_SINGLE)
    %assign uint8Name = LibGetDataTypeNameFromId(::CompiledModel.tSS_UINT8)
    %assign uint16Name = LibGetDataTypeNameFromId(::CompiledModel.tSS_UINT16)
    %assign msFcn = SLibGetMemSecCategoryForUtilityFcn()
    %openfile buf
    %<FcnGenOpenExternCForCPP()>
    %<SLibGetFcnMemSecPragmaOnDecl("rtGetNaN", msFcn, "Pre")>\
    %<LibStaticOrExternInFcnDecls()>%<doubleName> rtGetNaN(void);
    %<SLibGetFcnMemSecPragmaOnDecl("rtGetNaN", msFcn, "Post")>\
    %<SLibGetFcnMemSecPragmaOnDecl("rtGetNaNF", msFcn, "Pre")>\
    %<LibStaticOrExternInFcnDecls()>%<singleName> rtGetNaNF(void);
    %<SLibGetFcnMemSecPragmaOnDecl("rtGetNaNF", msFcn, "Post")>\
    %<FcnGenCloseExternCForCPP()>
    %closefile buf
    %<SLibCacheUtilCodeToFile("util_hdr_infnan_decl", fileH, buf)>

    %% Create source file contents
    %openfile buf
    /*
    * %<fctInfo.FileName>.%<LangFileExt>
    *
    %<SLibCommonUtilHeaderInfo()>\
    */
    %closefile buf
    %<SLibCacheUtilCodeToFile("util_src_banner", fileC, buf)>

    %openfile buf
    /*
    * Abstract:
    *      Function to initialize non-finite, NaN
    */
    #include "rtGetNaN.h"

    %closefile buf
    %<SLibCacheUtilCodeToFile("util_src_incl", fileC, buf)>

    %openfile buf
    #define NumBitsPerChar  %<::CompiledModel.ConfigSet.ProdBitPerChar>U
    %closefile buf
    %<SLibCacheMacroToUtilityFile("NumBitsPerChar", buf, "util_src_defines", fileC)>

    %assign scalarTokens = LibGetScalarDataInitTokens()

    %openfile buf
    %if GenCPP
        extern "C" {
    %endif
    %assign fcnName = "rtGetNaN"
    %assign fcnReturns = doubleName
    %assign fcnParams = "void"
    %assign functionSpecifier = LibStaticInFcnDecls()
    %openfile fcnAbstract
%%}
Initialize rtNaN needed by the generated code.
NaN is initialized as non-signaling. Assumes IEEE.
%%{
    %closefile fcnAbstract

    %createrecord fcnRec {Name fcnName; Returns fcnReturns; Params fcnParams; Abstract fcnAbstract; ...
        Category "utility"; GeneratedBy "genrtnan.tlc"}
    %<SLibDumpFunctionBanner(fcnRec)>
    %undef fcnRec
    %<SLibGetFcnMemSecPragma(fcnName, msFcn, "Pre")>\
    %<functionSpecifier> %<fcnReturns> %<fcnName>(%<fcnParams>) {
        size_t bitsPerReal%<scalarTokens[0]>sizeof(%<doubleName>) * (NumBitsPerChar)%<scalarTokens[1]>;
        %<doubleName> nan%<scalarTokens[0]>0.0%<scalarTokens[1]>;

        if (bitsPerReal == 32U) {
            nan = rtGetNaNF();
        } else {
            %if !SLibIsERTTarget() || ConfigSet.TargetEndianess == "Unspecified" || ConfigSet.PortableWordSizes == 1
                %<uint16Name> one = 1U;

                enum {
                    LittleEndian,
                    BigEndian
                } machByteOrder%<scalarTokens[0]>(*((%<uint8Name> *) &one) == 1U) ? LittleEndian : BigEndian%<scalarTokens[1]>;

                switch (machByteOrder) {
                    case LittleEndian: {
                        %<FcnLittleEndian()>
                        break;
                    }
                    case BigEndian: {
                        %<FcnBigEndian()>
                        break;
                    }
                }
            %elseif ConfigSet.TargetEndianess == "LittleEndian"
                %<FcnLittleEndian()>
            %elseif ConfigSet.TargetEndianess == "BigEndian"
                %<FcnBigEndian()>
            %else
                %openfile errTxt
                Unsupported Target Endianess: %<ConfigSet.TargetEndianess>
                %closefile errTxt
                %<LibReportFatalError(errTxt)>
            %endif
        }
        return nan;
    }
    %<SLibGetFcnMemSecPragma(fcnName, msFcn, "Post")>\

    %assign fcnName = "rtGetNaNF"
    %assign fcnReturns = singleName
    %assign fcnParams = "void"
    %assign functionSpecifier = LibStaticInFcnDecls()
    %openfile fcnAbstract
%%}
Initialize rtNaNF needed by the generated code.
NaN is initialized as non-signaling. Assumes IEEE.
%%{
    %closefile fcnAbstract

    %createrecord fcnRec {Name fcnName; Returns fcnReturns; Params fcnParams; Abstract fcnAbstract; ...
        Category "utility"; GeneratedBy "genrtnan.tlc"}
    %<SLibDumpFunctionBanner(fcnRec)>
    %undef fcnRec
    %assign equalSign = LibGetDataInitEqualSign()
    %<SLibGetFcnMemSecPragma(fcnName, msFcn, "Pre")>\
    %<functionSpecifier> %<fcnReturns> %<fcnName>(%<fcnParams>) {
        IEEESingle nanF%<equalSign>{ {0.0F} };
        %if !SLibIsERTTarget() || ConfigSet.TargetEndianess == "Unspecified" || ConfigSet.PortableWordSizes == 1
            %<uint16Name> one%<scalarTokens[0]>1U%<scalarTokens[1]>;

            enum {
                LittleEndian,
                BigEndian
            } machByteOrder%<scalarTokens[0]>(*((%<uint8Name> *) &one) == 1U) ? LittleEndian : BigEndian%<scalarTokens[1]>;

            switch (machByteOrder) {
                case LittleEndian: {
                    %% Using MATLAB values for NaN rather than IEEE
                    nanF.wordL.wordLuint = 0xFFC00000U;
                    break;
                }
                case BigEndian: {
                    nanF.wordL.wordLuint = 0x7FFFFFFFU;
                    break;
                }
            }
        %elseif ConfigSet.TargetEndianess == "LittleEndian"
            nanF.wordL.wordLuint = 0xFFC00000U;
        %elseif ConfigSet.TargetEndianess == "BigEndian"
            nanF.wordL.wordLuint = 0x7FFFFFFFU;
        %else
            %openfile errTxt
            Unsupported Target Endianess: %<ConfigSet.TargetEndianess>
            %closefile errTxt
            %<LibReportFatalError(errTxt)>
        %endif
        return nanF.wordL.wordLreal;
    }
    %<SLibGetFcnMemSecPragma(fcnName, msFcn, "Post")>\

    %if GenCPP
    }
    %endif

    %closefile buf
    %<SLibCacheUtilCodeToFile("util_src_fcn_defn", fileC, buf)>

    %assign headerFile = SLibGetFullFileNameForUtilCode("util_hdr_infnan_decl", fileH)
    %assign sourceFile = SLibGetFullFileNameForUtilCode("util_src_fcn_defn", fileC)
    %assign declDeps = [ "" ]

    %assign defnDeps = [ "size_t", "BigEndianIEEEDouble", "LittleEndianIEEEDouble" ]
    %<SLibAddEntryToFunctionTable("rtGetNaN", headerFile, sourceFile, declDeps, defnDeps)>

    %assign defnDeps = [ "IEEESingle" ]
    %<SLibAddEntryToFunctionTable("rtGetNaNF", headerFile, sourceFile, declDeps, defnDeps)>

    %if GenCPP
        %if ((GenUtilsSrcInSharedLocation == 1) || !SLibIsCompactFileFormat())
            %<SLibUpdateCLinkageHeader(headerFile)>
        %endif
    %endif


    %% Basic type headers for fixed width integer and floating point types (will
    %% resolve to e.g. rtwtypes.h, stdint.h or cstdint depdending on language
    %% standard types setting).
    %assign headerScopeFalse = TLC_FALSE
    %<SLibUpdateSymbolForFunction("rtGetNaN", SLibVarGroupBasicTypesFixedWidthInt(), headerScopeFalse)>
    %assign headerScopeTrue = TLC_TRUE
    %<SLibUpdateSymbolForFunction("rtGetNaN", SLibVarGroupBasicTypesOther(), headerScopeTrue)>

    %if ((GenUtilsSrcInSharedLocation == 1) || !SLibIsCompactFileFormat())
        %<SLibUpdateHeadersNeededByFile(headerFile, "rt_nonfinite.h")>
    %endif

%endfunction
