%% ==============================================================================
%%
%%
%%
%%
%% Abstract:
%%   Example main for bare board target (single rate model)
%%
%% Copyright 1994-2022 The MathWorks, Inc.
%%
%selectfile NULL_FILE

%function FcnSingleTaskingMain() void
    %assign booleanType = LibGetDataTypeNameFromId(::CompiledModel.tSS_BOOLEAN)
    %assign intType = LibGetDataTypeNameFromId(::CompiledModel.tSS_INTEGER)

    %<SetCurrentUtilsIncludesIdx("main_util_incl")>

    %if GenerateSampleERTMain
        %assign ::CompiledModel.OverrideSampleERTMain = TLC_TRUE
    %endif

    %openfile tmpBuf
    static %<booleanType> OverrunFlag = 0;\

    %<SLibDeclareFcnProtoCtlVariables()>\

    %<LibWriteModelData()>\
    %closefile tmpBuf

    %<SLibCacheCodeToFile("mainSrc_data_defn", tmpBuf)>

    %openfile tmpBuf
    #include "%<LibGetMdlPubHdrBaseName()>.h"
    %closefile tmpBuf

    %<SLibCacheCodeToFile("mainSrc_incl", tmpBuf)>

    %openfile tmpBuf
    %assign fcnReturns = "void"
    %assign fcnName = "rt_OneStep"
    %assign fcnParams = ""
    %assign fcnCategory = "main"
    %createrecord fcnRec {Name fcnName; Returns fcnReturns; Params fcnParams; ...
        Abstract ""; Category fcnCategory; GeneratedBy "bareboard_srmain.tlc"; ...
        Type "Utility"}
    %<SLibDumpFunctionBanner(fcnRec)>
    %undef fcnRec
    %<fcnReturns> %<fcnName>(%<fcnParams>)
    {
        /* Disable interrupts here */

        /* Check for overun */
        if (OverrunFlag++) {
            %<LibSetRTModelErrorStatus("\"Overrun\"")>;
            return;
        }

        /* Save FPU context here (if necessary) */
        /* Re-enable timer or interrupt here */
        %assign varsbuf = LibWriteModelInputs()
        %if varsbuf != ""
            /* Remove conditional, and set model inputs here */
            %<varsbuf>\
        %endif

        %<LibCallModelStep(0)>\

        %assign varsbuf = LibWriteModelOutputs()
        %if varsbuf != ""
            /* Remove conditional, and get model outputs here */
            %<varsbuf>\
        %endif

        OverrunFlag--;

        /* Disable interrupts here */
        /* Restore FPU context here (if necessary) */
        /* Enable interrupts here */
    }

    %assign fcnReturns = intType
    %assign fcnName = "main"
    %assign fcnParams = "%<intType> argc, const char *argv[]"
    %assign fcnCategory = "main"
    %createrecord fcnRec {Name fcnName; Returns fcnReturns; Params fcnParams; ...
        Abstract ""; Category fcnCategory; GeneratedBy "bareboard_srmain.tlc"; ...
        Type "Main"}
    %<SLibDumpFunctionBanner(fcnRec)>
    %undef fcnRec
    %<fcnReturns> %<fcnName>(%<fcnParams>)
    {

        /* Unused arguments */
        (void)(argc);
        (void)(argv);

        /* Initialize model */
        %<LibCallModelInitialize()>\

        /* Associate rt_OneStep() with a timer that executes at the base rate of the model */

        %<LibCallModelTerminate()>\
        return 0;
    }
    %closefile tmpBuf

    %<SLibCacheCodeToFile("mainSrc_fcn_defn", tmpBuf)>

    %<SetCurrentUtilsIncludesIdx("")>

%endfunction
